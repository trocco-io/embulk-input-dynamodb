plugins {
    id "scala"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.embulk.embulk-plugins" version "0.5.5"
    // note: Incompatible because this component declares an API of a component compatible with Java 11 and the consumer needed a runtime of a component compatible with Java 8
    // id "com.diffplug.spotless" version "6.15.0"
    // https://github.com/diffplug/spotless/blob/main/plugin-gradle/CHANGES.md#6130---2023-01-14
    id "com.diffplug.spotless" version "6.13.0"
    // note: We cannot use the latest version because of the following error.
    // > org/eclipse/jgit/lib/Repository has been compiled by a more recent version of the Java Runtime (class file version 55.0), this version of the Java Runtime only recognizes class file versions up to 52.0
    // id "com.palantir.git-version" version "0.15.0"
    id "com.palantir.git-version" version "0.12.3"
}
repositories {
    mavenCentral()
}
group = "pro.civitaspo"
description = "Loads records from Dynamodb."
version = {
    def vd = versionDetails()
    if (vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.43"
    def awsSdkVersion = "2.20.26"
    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"
    implementation "org.scala-lang:scala-library:2.13.10"

    implementation "org.embulk:embulk-util-config:0.3.2"
    implementation "org.embulk:embulk-util-json:0.3.0"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation("software.amazon.awssdk:dynamodb:${awsSdkVersion}") {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
    }
    implementation("software.amazon.awssdk:sts:${awsSdkVersion}") {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
    }
    implementation("software.amazon.awssdk:apache-client:${awsSdkVersion}") {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
    }
    
    // Use Embulk's Jackson (compileOnly to avoid bundling)
    compileOnly "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compileOnly "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compileOnly "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compileOnly "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-input-config:0.10.36"
    
    // JAXB dependencies for Java 11+
    testImplementation "javax.xml.bind:jaxb-api:2.3.1"
    testImplementation "org.glassfish.jaxb:jaxb-runtime:2.3.1"
    testImplementation "javax.activation:activation:1.1.1"
}
test {
    jvmArgs '-Xms4g', '-Xmx4g', '-XX:MaxMetaspaceSize=1g'
    maxHeapSize = "4g"
}
shadowJar {
    archiveClassifier.set('')
    // Exclude Embulk provided dependencies and Jackson (use Embulk's Jackson)
    dependencies {
        exclude(dependency('org.embulk:embulk-api'))
        exclude(dependency('org.embulk:embulk-spi'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-core'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-databind'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-annotations'))
        exclude(dependency('com.fasterxml.jackson.datatype:jackson-datatype-jdk8'))
    }
}

spotless {
    scala {
        scalafmt('3.7.1').configFile('.scalafmt.conf')
    }
}

embulkPlugin {
    mainClass = "org.embulk.input.dynamodb.DynamodbInputPlugin"
    category = "input"
    type = "dynamodb"
}

// Use shadowJar output for the gem
jar {
    enabled = false
    dependsOn shadowJar
}

// Make shadowJar output available as the main artifact
artifacts {
    archives shadowJar
}

// Configure gem task to use shadowJar
tasks.whenTaskAdded { task ->
    if (task.name == 'gem') {
        task.dependsOn shadowJar
    }
}
publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}
gem {
    from("LICENSE.txt")
    authors = ["Daisuke Higashi", "Civitaspo"]
    email = ["daisuke.develop@gmail.com", "civitaspo@gmail.com"]
    summary = "An Embulk plugin to ingest data from Dynamodb."
    homepage = "https://github.com/lulichn/embulk-input-dynamodb"
    licenses = [ "MIT" ]
}
